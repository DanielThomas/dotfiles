ZSH=$HOME/.oh-my-zsh
DOTFILES=$HOME/.dotfiles

# oh-mh-zsh theme
ZSH_THEME="agnoster"

# oh-my-zsh aliases
alias zshconfig="st ~/.zshrc"
alias ohmyzsh="st ~/.oh-my-zsh"

# define oh-my-zsh plugins implicitly based on the topics
plugins=("${(@f)$(find $DOTFILES -not -name '.git' -d 1 -type d -exec basename {} \;)}")

# add any plugins defined by files
plugins+=("${(@f)$(find $DOTFILES -name oh-my-zsh.plugins -d 2 -exec cat {} \;)}")

# load oh-my-zsh
source $ZSH/oh-my-zsh.sh

# use .localrc for things that need to be kept secret
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# make sure DEFAULT_USER has been set
if [ -z "$DEFAULT_USER" ]; then
  echo "Please set DEFAULT_USER in your ~/.localrc"
  exit 1
fi

# all of our zsh files
typeset -U config_files
config_files=($DOTFILES/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files
